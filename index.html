<!DOCTYPE html>
<html class='no-js'>
    <head>
        <meta charset='utf-8'>
        <meta http-equiv='X-UA-Compatible' content='IE=edge,chrome=1'>
        <title>Life</title>
        <meta name='description' content=''>
        <meta name='viewport' content='width=device-width'>

        <link rel='stylesheet' href='css/normalize.min.css'>
        <link rel='stylesheet' href='css/main.css'>

        <script src='js/vendor/jquery-2.1.0.min.js'></script>
        <script src='js/vendor/jquery.mousewheel.js'></script>
        <script src="js/vendor/dat.gui.min.js"></script>
        <script src='js/vendor/stats.js'></script>

        <script type="text/javascript">

            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-48809051-1']);
            _gaq.push(['_trackPageview']);

            (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();

        </script>
    </head>
    <body>

        <div id="info-container">
            <div class="info-control">
                <strong>Pan:</strong> Hold right click
                <br>
                <strong>Erase:</strong> Hold Shift and draw
            </div>
            <div class="info-control">
                <strong>Zoom:</strong> Use mousewheel
                <br>
                <strong>Solid Brush:</strong> Hold Ctrl and draw
            </div>
        </div>

        <canvas id='canvas' oncontextmenu="return false;"></canvas>

        <script id='vertex-shader' type='x-shader/x-vertex'>
            attribute vec2 a_position;

            varying vec2 v_texCoord;


            void main() {
                vec2 clipSpace = a_position * 2.0 - 1.0;
               
                gl_Position = vec4(clipSpace, 0, 1);

                v_texCoord = a_position;
            }
        </script>
        
        <script id='cell-iteration-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_bufferResolution;
            uniform vec4 u_surface;
            uniform sampler2D u_buffer;
            uniform sampler2D u_rules;

            varying vec2 v_texCoord;

            #define RULES_COUNT 8.0

            #define FIRST_DECAY vec4( vec3(0.2), 1)
            #define DECAY vec4( vec3(0.002), 1)

            vec2 onePixel = vec2(1.0, 1.0) / u_bufferResolution;

            vec4 isAlive(int x, int y) {
                vec4 pixel = texture2D(u_buffer, fract(v_texCoord + vec2(x, y) * onePixel));

                if (pixel.a == 1.) 
                    return pixel;
                else 
                    return vec4(0, 0, 0, 0);
            }

            void main() {


                vec4 n = isAlive(0, 1)
                       + isAlive(1, 1)
                       + isAlive(1, 0)
                       + isAlive(1, -1)
                       + isAlive(0, -1)
                       + isAlive(-1, -1)
                       + isAlive(-1, 0)
                       + isAlive(-1, 1);

                // alive
                if (isAlive(0, 0).a == 1.) {
                    if ( texture2D(u_rules, vec2(n.a / RULES_COUNT, 0) ).r > 0. )
                        gl_FragColor = texture2D(u_buffer, v_texCoord);
                    else
                        gl_FragColor = texture2D(u_buffer, v_texCoord) - FIRST_DECAY;

                }

                // dead
                else {
                    if ( texture2D(u_rules, vec2(n.a / RULES_COUNT, 0) ).g > 0. )
                        gl_FragColor = ( n.a >= 1. ? 
                                         n / vec4(n.a, n.a, n.a, 1) :
                                         vec4(0.5, 0.5, 0.5, 1) );
                    else 
                        gl_FragColor = texture2D(u_buffer, v_texCoord)- DECAY;
                }
            }
        </script>

        <script id='mouse-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_bufferResolution;
            uniform vec2 u_mouse;
            uniform vec4 u_color;
            uniform float u_paintSize;
            uniform bool u_paintType[3]; //{ pixel, eraser, solid }
            uniform float u_random;
            uniform vec4 u_surface;
            uniform sampler2D u_buffer;

            varying vec2 v_texCoord;

            float rand(vec2 co) {
                return fract(sin(dot(fract(co.xy + u_random), vec2(12.9898,78.233))) * 43758.5453);
            }

            void main() {

                vec2 mouseCoord = fract(u_surface.wz + u_mouse * (u_surface.yx - u_surface.wz));
                vec2 distance = abs(mouseCoord - v_texCoord);
                
                if (u_paintType[0]) {

                    distance *= u_bufferResolution;
                    
                    if ( dot(distance, distance) < 0.25)
                        gl_FragColor = u_color;
                    else 
                        gl_FragColor = texture2D(u_buffer, v_texCoord);
                }

                else {

                    distance = min(distance, 1. - distance);
                    distance.x *= u_bufferResolution.x / u_bufferResolution.y;

                    if ( dot(distance, distance) < u_paintSize) {

                        if (u_paintType[1]) 
                            gl_FragColor = vec4(0.08, 0.08, 0.08, 0);

                        else if (u_paintType[2]) 
                            gl_FragColor = u_color;
      
                        else 
                            gl_FragColor = rand(gl_FragCoord.xy / u_bufferResolution) < 0.5 ? u_color : vec4(u_color.rgb * 0.7, 0);
                    }
                    else 
                        gl_FragColor = texture2D(u_buffer, v_texCoord);  
                }  
            }

        </script>

        <script id='screen-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform sampler2D u_screenBuffer;
            uniform vec4 u_surface; //top, right, bottom left

            varying vec2 v_texCoord;

            void main() {

                vec2 uv = u_surface.wz + v_texCoord * (u_surface.yx - u_surface.wz);
                gl_FragColor = texture2D(u_screenBuffer, fract(uv));
            }

        </script>

        <script src='js/rule-presets.js'></script>
        <script src='js/main.js'></script>
    </body>
</html>
