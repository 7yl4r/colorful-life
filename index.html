<!DOCTYPE html>
<html class='no-js'>
    <head>
        <meta charset='utf-8'>
        <meta http-equiv='X-UA-Compatible' content='IE=edge,chrome=1'>
        <title>Life</title>
        <meta name='description' content=''>
        <meta name='viewport' content='width=device-width'>

        <link rel='stylesheet' href='css/normalize.min.css'>
        <link rel='stylesheet' href='css/main.css'>

        <script src='js/vendor/dat.gui.min.js'></script>
        <script src='js/vendor/stats.js'></script>

        <script type='text/javascript'>

            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-48809051-1']);
            _gaq.push(['_trackPageview']);

            (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();

        </script>
    </head>
    <body>

        <div id='overlay'>
            <h1> Draw something. </h1>
            <div id='controls'>
                <span>
                    <strong>Pan:</strong> Hold right click
                    <br>
                    <strong>Eraser:</strong> Hold Shift and draw
                </span>
                <span>
                    <strong>Zoom:</strong> Use mousewheel
                    <br>
                    <strong>Solid Brush:</strong> Hold Ctrl and draw
                </span>
            </div>
        </div>

        <canvas id='canvas'></canvas>

        <script type='text/javascript'>
        
            var canvas = document.getElementById('canvas');
            
            canvas.oncontextmenu = function() {
                return false;
            };

            function hideOverlay(e) {
                if (e.which === 1) {
                    document.getElementById('overlay').classList.add('hidden');
                    e.target.removeEventListener(e.type, hideOverlay);
                }
            }
            canvas.addEventListener('mousedown', hideOverlay);
        </script>

        <script id='vertex-shader' type='x-shader/x-vertex'>
            attribute vec2 a_position;

            varying vec2 v_texCoord;


            void main() {
                vec2 clipSpace = a_position * 2.0 - 1.0;
               
                gl_Position = vec4(clipSpace, 0, 1);

                v_texCoord = a_position;
            }
        </script>
        
        <script id='cell-iteration-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_bufferResolution;
            uniform vec4 u_surface;
            uniform sampler2D u_buffer;
            uniform sampler2D u_rules;

            varying vec2 v_texCoord;

            #define RULES_COUNT 8.0
            #define FIRST_DECAY vec4( vec3(0.2), 1)
            #define DECAY vec4( vec3(0.002), 1)

            vec2 onePixel = vec2(1.0, 1.0) / u_bufferResolution;

            vec4 isAlive(int x, int y) {
                vec4 pixel = texture2D(u_buffer, fract(v_texCoord + vec2(x, y) * onePixel));

                return mix(vec4(0, 0, 0, 0), pixel, pixel.a);
            }

            void main() {

                vec4 n = isAlive(0, 1)
                       + isAlive(1, 1)
                       + isAlive(1, 0)
                       + isAlive(1, -1)
                       + isAlive(0, -1)
                       + isAlive(-1, -1)
                       + isAlive(-1, 0)
                       + isAlive(-1, 1);

                // alive
                if (isAlive(0, 0).a == 1.) {
                    float state = texture2D(u_rules, vec2(n.a / RULES_COUNT, 0)).r;
                    gl_FragColor = texture2D(u_buffer, v_texCoord) - (1.0 - state) * FIRST_DECAY;
                }

                // dead
                else {
                    float state = texture2D(u_rules, vec2(n.a / RULES_COUNT, 0) ).g;
                    gl_FragColor = mix(texture2D(u_buffer, v_texCoord) - DECAY, 
                                       n / vec4(n.a, n.a, n.a, 1),
                                       state);
                }
            }
        </script>

        <script id='mouse-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform vec2 u_bufferResolution;
            uniform vec2 u_mouse;
            uniform vec4 u_color;
            uniform float u_paintSize;
            uniform bool u_paintErase;
            uniform bool u_paintSolid;
            uniform bool u_paintPixel;
            uniform float u_random;
            uniform vec4 u_surface;
            uniform sampler2D u_buffer;

            varying vec2 v_texCoord;

            float rand(vec2 co) {
                return fract(sin(dot(fract(co.xy + u_random), vec2(12.9898,78.233))) * 43758.5453);
            }

            void main() {

                vec2 mouseCoord = fract(u_surface.wz + u_mouse * (u_surface.yx - u_surface.wz));
                vec2 distance = abs(mouseCoord - v_texCoord);
                
                if (u_paintPixel) {

                    distance *= u_bufferResolution;
                    
                    if ( dot(distance, distance) < 0.25 )
                        gl_FragColor = u_paintErase ? vec4(0, 0, 0, 0) : u_color;
                    else 
                        gl_FragColor = texture2D(u_buffer, v_texCoord);
                }

                else {

                    distance = min(distance, 1. - distance);
                    distance.x *= u_bufferResolution.x / u_bufferResolution.y;

                    if ( dot(distance, distance) < u_paintSize ) {

                        if (u_paintErase) 
                            gl_FragColor = vec4(0.08, 0.08, 0.08, 0);

                        else if (u_paintSolid) 
                            gl_FragColor = u_color;
      
                        else 
                            gl_FragColor = rand(gl_FragCoord.xy / u_bufferResolution) < 0.5 ? u_color : vec4(u_color.rgb * 0.7, 0);
                    }
                    else 
                        gl_FragColor = texture2D(u_buffer, v_texCoord);  
                }  
            }

        </script>

        <script id='screen-shader' type='x-shader/x-fragment'>
            precision mediump float;

            uniform sampler2D u_screenBuffer;
            uniform vec4 u_surface; //top, right, bottom left

            varying vec2 v_texCoord;

            void main() {

                vec2 uv = u_surface.wz + v_texCoord * (u_surface.yx - u_surface.wz);
                gl_FragColor = texture2D(u_screenBuffer, fract(uv));
            }

        </script>

        <script src='js/rule-presets.js'></script>
        <script src='js/main.js'></script>
    </body>
</html>
